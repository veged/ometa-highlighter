ometa OmetaHighlighterJs {
    fromTo :x :y = seq(x):s (~seq(y) char)*:c seq(y):e -> [s, c.join(''), e],

    nameFirst = letter | '$' | '_',
    nameRest = nameFirst | digit,
    name = firstAndRest(`nameFirst, `nameRest):r -> [`name, r.join('')],

    builtin = ( ``Array'' | ``Boolean'' | ``Date''
              | ``Error'' | ``Function'' | ``Math''
              | ``netscape'' | ``Number'' | ``Object''
              | ``Packages'' | ``RegExp'' | ``String''
              | ``sun'' | ``decodeURI''
              | ``decodeURIComponent'' | ``encodeURI''
              | ``encodeURIComponent'' | ``Error''
              | ``eval'' | ``isFinite'' | ``isNaN''
              | ``parseFloat'' | ``parseInt''
              | ``document'' | ``this'' | ``window''):c -> ['name.builtin', c],


    declaration = (``var'' | ``with'' | ``function''):c -> ['keyword.declaration', c],
    reserved = ( ``abstract'' | ``boolean'' | ``byte''
               | ``char'' | ``class'' | ``const''
               | ``debugger'' | ``double'' | ``enum''
               | ``export'' | ``extends'' | ``final''
               | ``float'' | ``goto'' | ``implements''
               | ``import'' | ``int'' | ``interface''
               | ``long'' | ``native'' | ``package''
               | ``private'' | ``protected''
               | ``public'' | ``short'' | ``static''
               | ``super'' | ``synchronized''
               | ``throws'' | ``transient'' | ``volatile''):c -> ['keyword.reserved', c],
    constant = ( ``true'' | ``false'' | ``null''
               | ``NaN'' | ``Infinity'' | ``undefined''):c -> ['keyword.constant', c],

    keyword = declaration | reserved | constant |
              ( ``for'' | ``in'' | ``while'' | ``do'' | ``break''
              | ``return'' | ``continue'' | ``switch'' | ``case''
              | ``default'' | ``if'' | ``else'' | ``throw''
              | ``try'' | ``catch'' | ``finally'' | ``new''
              | ``delete'' | ``typeof''
              | ``instanceof'' | ``void'' | ``this''):c -> [`keyword, c],


    // TODO: unicode char, octal char
    escapeChar = '\\':e char:c -> ['string.escape', [e, c]],
    nonEscapeChar :q = ~seq(q) char,
    quote :q = seq(q):c -> [`punctuation, c],
    anyString :q :n = quote(q):qp
                      (escapeChar | nonEscapeChar(q))*:cs
                      quote(q) -> { cs.unshift(qp); cs.push(qp); [`string + '.' + n, cs] },

    // TODO: modifiers (ff y or any)
    // https://developer.mozilla.org/en/JavaScript/Guide/Regular_Expressions#Advanced_Searching_With_Flags
    string = anyString('\'', `single)
             | anyString('"', `double)
             | (anyString('/', `regex):r (`g | `i | `m)*:f -> { r[1] = r[1].concat(f); r }),


    // TODO: exponent
    integer = digit+:c -> ['number.integer', c],
    float = digit*:ws '.' digit*:fs ?(ws.length + fs.length != 0) -> ['number.float', ws.concat([[`punctuation, '.']]).concat(fs)],
    hex = '0' 'x' (digit | `a | `b | `c | `d | `e | `f)*:c -> ['number.hex', ['0'].concat([[`punctuation, 'x']]).concat(c)],
    number = float | hex | integer,

    comment = (``//'':p (~'\n' char)*:c) -> ['comment.single', [[`punctuation, p]].concat(c)]
              | fromTo('/*', '*/'):b -> ['comment.multiline', [[`punctuation, b[0]]].concat(b[1]).concat([[`punctuation, b[2]]])],


    operator = ( '?'    | ':'     | ``!=='' | ``!=''  | ``==='' | ``=='' | ``=''  | ``>=''
               | '>'    | ``<=''  | '<'     | ``++''  | ``+=''  | '+'    | ``--'' | ``-=''
               | '-'    | ``*=''  | '*'     | ``/=''  | '/'     | ``%='' | '%'    | ``&&=''
               | ``&&'' | ``||='' | ``||''  | '.'     | '!'                                ):c -> [`operator, [c]],

    punctuation = ( '(' | ')' | '{' | '}' | '[' | ']' | ',' | ';' ):c -> [`punctuation, [c]],

    nonText = (keyword:c ~nameRest) -> c | name | comment | string | number | operator | punctuation,
    topLevel = (nonText | char)*
}

ometa OmetaHighlighterXml {
    fromTo :x :y = seq(x):s (~seq(y) char)*:c seq(y):e -> [s, c.join(''), e],

    nameFirst = letter | ':' | '_',
    nameRest = nameFirst | digit | '-' | '.',
    name = firstAndRest(`nameFirst, `nameRest):r -> [`name, r.join('')],

    quote :q = seq(q):c -> [`punctuation, c],
    anyString :q :n = quote(q):qp
                      (~seq(q) char)*:cs
                      quote(q) -> { cs.unshift(qp); cs.push(qp); [`string + '.' + n, cs] },

    preproc = (fromTo('<?', '?>') | fromTo('<![CDATA[', ']]>')):c
              -> ['comment.preproc', [[`punctuation, c[0]]].concat(c[1]).concat([[`punctuation, c[2]]])],

    string = anyString('\'', `single)
             | anyString('"', `double),

    attr = name:n spaces:s1 '=':p spaces:s2 string:s -> ['name.attribute', [n[1]].concat(s1).concat([[`punctuation, p]]).concat(s2).concat([s])],

    tag = '<':p1 spaces:s1 name:n
              ((spaces:s2 attr:a spaces:s3 -> s2.concat([a]).concat(s3))*:as -> Array.prototype.concat.apply([], as)):as
              spaces:s4 ('>' | ``/>''):p2
              -> ['name.tag', [[`punctuation, p1]].concat(s1).concat(n[1]).concat(as).concat(s4).concat([[`punctuation, p2]])]
          | '<':p1 spaces:s1 '/':p2 name:n spaces:s2 '>':p3 -> ['name.tag', [[`punctuation, p1 + p2]].concat(s1).concat(n[1]).concat(s2).concat([[`punctuation, p3]])],

    entity = '&':p1 name:n ';':p2 -> ['name.entity', [[`punctuation, p1]].concat(n[1]).concat([[`punctuation, p2]])],
    comment = fromTo('<!--', '-->'):c -> ['comment', [[`punctuation, c[0]]].concat(c[1]).concat([[`punctuation, c[2]]])],

    nonText = comment | preproc | entity | tag,
    topLevel = (nonText | char)*
}

ometa OmetaHighlighter {
    js = char*:c -> [`js, OmetaHighlighterJs.matchAll(c.join(''), 'topLevel')],
    javascript = js,
    xml = char*:c -> [`xml, OmetaHighlighterXml.matchAll(c.join(''), 'topLevel')]
}
